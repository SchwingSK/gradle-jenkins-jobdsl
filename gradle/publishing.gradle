apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

publishing {
    publications {
        main(MavenPublication) {
            if (plugins.hasPlugin('war')) {
                from components.web
            } else {
                from components.java
            }
            artifact sourcesJar
            artifact javadocJar
            artifact groovydocJar

            pom.withXml {
                asNode().with {
                    appendNode('name', 'Jenkins JobDSL plugin for Gradle')
                    appendNode('description', 'A Gradle plugin that let\'s you build and generate your Jenkins JobDSL scripts')
                    appendNode('url', 'http://github.com/pvdissel/gradle-jenkins-jobdsl')
                    appendNode('licenses').with {
                        appendNode('license').with {
                            appendNode('name', 'The Apache Software License, Version 2.0')
                            appendNode('url', 'http://www.apache.org/licenses/LICENSE-2.0')
                        }
                    }
                    appendNode('developers').with {
                        appendNode('developer').with {
                            appendNode('id', 'pvdissel')
                            appendNode('name', 'Patrick van Dissel')
                            appendNode('email', 'pvdissel@gmail.com')
                        }
                    }
                    appendNode('scm').with {
                        appendNode('connection', 'git://github.com/pvdissel/gradle-jenkins-jobdsl.git')
                        appendNode('developerConnection', 'git@github.com:pvdissel/gradle-jenkins-jobdsl.git')
                        appendNode('url', 'http://github.com/pvdissel/gradle-jenkins-jobdsl')
                    }
                }
            }
        }
    }
    repositories {
        mavenLocal()
    }
}

if (project.hasProperty('bintrayUser') && project.hasProperty('bintrayKey')) {
    bintray {
        user = project.bintrayUser
        key = project.bintrayKey
        publications = ['main']
        dryRun = false
        publish = false
        pkg {
            repo = 'gradle-plugins'
            name = 'nl.ikoodi.gradle.plugin:gradle-jenkins-jobdsl'
            desc = 'A Gradle plugin that let\'s you build and generate your Jenkins JobDSL scripts'
            licenses = ['Apache-2.0']
            labels = ['gradle', 'plugins']
            websiteUrl = 'https://github.com/pvdissel/gradle-jenkins-jobdsl'
            issueTrackerUrl = 'https://github.com/pvdissel/gradle-jenkins-jobdsl/issues'
            vcsUrl = 'https://github.com/pvdissel/gradle-jenkins-jobdsl.git'
            publicDownloadNumbers = true
        }
//        version {
//            name = project.version
//            desc = 'optional, version-specific description'
//            released  = 'optional, date of the version release' //2 possible values: date in the format of 'yyyy-MM-dd'T'HH:mm:ss.SSSZZ' OR a java.util.Date instance
//            vcsTag = "v${project.version}"
//            attributes = [
//                    'gradle-plugin': 'nl.ikoodi.jenkins.jobdsl:nl.ikoodi.gradle.plugin:gradle-jenkins-jobdsl'
//            ]
//            gpg {
//                sign = true
////                passphrase = 'optional, the passphrase for GPG signing'
//            }
//        }
    }
}

bintrayUpload.dependsOn('generatePomFileForMainPublication', 'build')
